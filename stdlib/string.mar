import stdlib.mar

| Chars  
| A Char (short for Character) is a wrapper around a Byte. When you write
| something like #a in your code, that gets turned into a Char. Don't confuse
| the Char with a Unicode code point, grapheme cluster, or anything like that.
| This is as much a "character" as the C char. But who are we to judge?
| Character is a heavily overloaded term anyways.

struct Char { byte: Byte }

var space = # 
var tab = 9.lower_byte().to_char()
var newline = 10.lower_byte().to_char()
var carriage_return = 13.lower_byte().to_char()
var visible_ascii = 32.lower_byte().to_char()..127.lower_byte().to_char()

var lowercase_chars = #a..=#z
var uppercase_chars = #A..=#Z
var digit_chars = #0..=#9

fun to_char(byte: Byte): Char { Char { byte } }

fun is_lower(char: Char): Bool { lowercase_chars.contains(char) }
fun is_upper(char: Char): Bool { uppercase_chars.contains(char) }
fun is_letter(char: Char): Bool { char.is_lower() or char.is_upper() }
fun is_digit(char: Char): Bool { digit_chars.contains(char) }
fun is_alphanumeric(char: Char): Bool { char.is_letter() or char.is_digit() }
fun is_whitespace(char: Char): Bool {
  char == space or char == newline or char == tab
}

fun parse_digit(char: Char): Byte {
  if char.is_digit() then return char.byte - #0.byte
  if char.is_lower() then return char.byte - #a.byte + 16.lower_byte()
  if char.is_upper() then return char.byte - #A.byte + 16.lower_byte()
  panic("not a digit")
}
fun <=>(a: Char, b: Char): Ordering { a.byte <=> b.byte }
fun ==(a: Char, b: Char): Bool { a.byte == b.byte }
fun hash(hasher: &Hasher, char: Char) { hasher.hash(char.byte) }
fun write[W](writer: W, char: Char) { writer.write_byte(char.byte) }
fun write_debug[W](writer: W, char: Char) { writer."#{char}" }
fun inc(char: Char): Char { Char { byte = char.byte.inc() } }
fun dec(char: Char): Char { Char { byte = char.byte.dec() } }
fun +(char: Char, delta: Byte): Char { Char { byte = char.byte + delta } }
fun -(char: Char, delta: Byte): Char { Char { byte = char.byte - delta } }
fun -(char: Char, other: Char): Byte { char.byte - other.byte }

fun fuzzing_complexity(char: Char): Float {
  if char.is_lower() then
    return 1.0 + {{char - #a}.to_int().to_float() / 26.0 / 10.0}
  if char.is_upper() then
    return 1.2 + {{char - #A}.to_int().to_float() / 26.0 / 10.0}
  if char.is_letter() then return 1.5
  if char.is_alphanumeric() then return 1.7
  if visible_ascii.contains(char) then return 2.0
  3.0
}

| Strings  
| A String (short for String) is just a wrapper around a Slice[Byte] and it's what
| what you get from string literals such as "blub". Try printing it! It gets
| formatted nicely.

struct String { data: Address, len: Int }

fun to_string(chars: Slice[Char]): String {
  chars.unchecked_cast[Slice[Char], String]()
}
fun to_string(bytes: Slice[Byte]): String {
  bytes.unchecked_cast[Slice[Byte], String]()
}
fun to_string(chars: List[Char]): String { chars.to_slice().to_string() }
fun chars(string: String): Slice[Char] {
  string.unchecked_cast[String, Slice[Char]]()
}
fun bytes(string: String): Slice[Byte] {
  string.unchecked_cast[String, Slice[Byte]]()
}

fun is_empty(string: String): Bool { string.len == 0 }
fun get_maybe_ref(string: String, index: Int): Maybe[&Char] {
  string.chars().get_maybe_ref(index)
}
fun get_ref(string: String, index: Int): &Char {
  string.get_maybe_ref(index) or panic("index out of bounds")
}
fun get_maybe(string: String, index: Int): Maybe[Char] {
  switch string.get_maybe_ref(index)
  case some(c) some(c.*)
  case none none[Char]()
}
fun get(string: String, index: Int): Char { string.get_ref(index).* }
fun get_unchecked(string: String, index: Int): Char asm {
  moveib a 8  add a sp load a a | string.data
  moveib b 24 add b sp load b b | index
  add a b load a a | char
  load c sp store c a ret
}

fun <=>(a: String, b: String): Ordering {
  switch memcompare(a.data, b.data, min(a.len, b.len))
  case less Ordering.less
  case greater Ordering.greater
  case equal if a.len < b.len then Ordering.less else Ordering.greater
}
fun ==(a: String, b: String): Bool {
  a.len == b.len or return false
  memcompare(a.data, b.data, a.len) is equal
}
fun hash(hasher: &Hasher, string: String) {
  hasher.hash(memhash(string.data, string.len))
}
fun write[W](writer: W, string: String) { writer.write_bytes(string.bytes()) }
fun write_debug[W](writer: W, string: String) {
  writer."\""
  for char in string do
    if char == #" then writer."\\\""
    else if char == #\ then writer."\\\\"
    else if char == newline then writer."\\n"
    else writer."{char}"
  writer."\""
}
fun copy(string: String): String {
  string.bytes().copy().to_string()
}

fun generate(s: Static[Char], random: &Random, complexity: Int): Char {
  var visible_ascii = 32..127
  random.next_int(visible_ascii).lower_byte().to_char()
}
fun mutate(char: Char, random: &Random, temperature: Int): Char {
  static[Char]().generate(random, 0)
}
fun fuzzing_complexity(string: String): Float {
  var chars_complexity = 0.0
  for char in string.chars() do
    chars_complexity = chars_complexity + char.fuzzing_complexity()
  string.len.to_float() + chars_complexity
}
fun generate(s: Static[String], random: &Random, complexity: Int): String {
  var len = random.next_int(0..=complexity)
  var slice = uninitialized_slice[Char](len)
  for i in 0..len do slice.get_ref(i).* = static[Char]().generate(random, 1)
  String { data = slice.data, len }
}
fun mutate(string: String, random: &Random, temperature: Int): String {
  if temperature == 0 then return string
  string.chars().mutate(random, temperature).to_string()
}

fun iter(string: String): Iter[Char, _] {
  Iter[Char, StrIter] { state = StrIter { string, index = 0 } }
}
struct StrIter { string: String, index: Int }
fun next(iter: &Iter[Char, StrIter]): Maybe[Char] {
  var state = iter.state.&
  if state.index >= state.string.len then none[Char]() else {
    var char = state.string.chars().get(state.index)
    state.index = state.index.inc()
    some(char)
  }
}

fun substr(string: String, range: Range[Int]): String {
  range.end <= string.len or panic("substr needs to be inside original string")
  string.chars().subslice(range).to_string()
}
fun first(string: String): Char {
  string.is_not_empty() or panic("first only works on non-empty strs")
  string.get(0)
}
fun first(string: String, amount: Int): String {
  amount <= string.len or panic("first amount too big")
  string.substr(0..amount)
}
fun last(string: String): Char {
  string.is_not_empty() or panic("last only works on non-empty strs")
  string.get(string.len.dec())
}
fun last(string: String, amount: Int): String {
  amount <= string.len or panic("last amount too big")
  string.substr({string.len - amount}..string.len)
}
fun without_first(string: String, amount: Int): String {
  amount <= string.len or panic("without_first amount too big")
  string.substr(amount..string.len)
}
fun without_last(string: String, amount: Int): String {
  amount <= string.len or panic("without_last amount too big")
  string.substr(0..{string.len - amount})
}
fun starts_with(string: String, prefix: String): Bool {
  string.len >= prefix.len or return false
  memcompare(string.data, prefix.data, prefix.len) is equal
}
fun ends_with(string: String, suffix: String): Bool {
  string.len >= suffix.len or return false
  memcompare(string.data + {string.len - suffix.len}, suffix.data, suffix.len)
    is equal
}
fun without_prefix(string: String, prefix: String): String {
  string.starts_with(prefix)
    or panic("without_prefix doesn't match prefix {prefix.debug()}")
  string.without_first(prefix.len)
}
fun without_suffix(string: String, suffix: String): String {
  string.ends_with(suffix)
    or panic("without_suffix doesn't match suffix {suffix.debug()}")
  string.without_last(suffix.len)
}
fun trim_prefix(string: &String, prefix: String) {
  string.* = string.without_prefix(prefix)
}
fun trim_suffix(string: &String, suffix: String) {
  string.* = string.without_suffix(suffix)
}
fun trim(string: String): String {
  var start = 0
  var end = string.len
  loop if start < string.len and string.get(start).is_whitespace()
    then start = start + 1
    else break
  loop if end > 0 and string.get(end - 1).is_whitespace() and not(start == end)
    then end = end - 1
    else break
  string.substr(start..end)
}
fun contains(string: String, needle: Char): Bool {
  string.chars().iter().&.contains(needle)
}
fun contains(string: String, needle: String): Bool {
  if needle.len > string.len then return false
  for i in 0..={string.len - needle.len} do
    if string.substr(i..+needle.len) == needle then return true
  false
}
fun replace(string: String, from: String, to: String): String {
  var b = string_builder().&
  var cursor = 0
  loop if cursor == string.len then break else {
    if string.without_first(cursor).starts_with(from) then {
      b."{to}"
      cursor = cursor + from.len
    } else {
      b."{string.chars().get(cursor)}"
      cursor = cursor + 1
    }
  }
  b.to_string()
}
fun split(string: String, separator: String): Slice[String] {
  var parts = list[String]()
  for part in string.iter().split(separator.chars()) do
    parts.&.push(part.to_string())
  parts.to_slice()
}
fun lines(string: String): Slice[String] { string.split("{newline}") }
fun pad_right(string: String, len: Int, char: Char): String {
  var b = string_builder().&
  b.write(string)
  loop if b.len() < len then b.write(char) else break
  b.to_string()
}
fun pad_right(string: String, len: Int): String { string.pad_right(len, space) }
fun pad_left(string: String, len: Int, char: Char): String {
  var b = string_builder().&
  loop if b.len() + string.len < len then b.write(char) else break
  b.write(string)
  b.to_string()
}
fun pad_left(string: String, len: Int): String { string.pad_left(len, space) }

fun join(items: Slice[String], separator: String): String {
  var b = string_builder().&
  var is_first = true
  for item in items do {
    if is_first then is_first = false else b."{separator}"
    b."{item}"
  }
  b.to_string()
}
fun reverse(a: String): String {
  var b = string_builder().&
  for char in a.chars().rev_iter() do b."{char}"
  b.to_string()
}

| String Building  
| To build strings efficiently, we need some sort of growable buffer where we
| can append more data over time. That's exactly what a StringBuilder is.

struct StringBuilder { bytes: List[Byte] }

fun string_builder(): StringBuilder { StringBuilder { bytes = list[Byte]() } }
fun write_byte(builder: &StringBuilder, byte: Byte) {
  builder.bytes.&.push(byte)
}
fun write_bytes(builder: &StringBuilder, bytes: Slice[Byte]) {
  builder.bytes.&.push_all(bytes)
}
fun len(builder: StringBuilder): Int { builder.bytes.len }
fun truncate_to_len(builder: &StringBuilder, len: Int) {
  len <= builder.bytes.len or panic("new len is too big")
  builder.bytes.len = len
}
fun write[W](writer: W, sb: StringBuilder) {
  writer.write_bytes(sb.bytes.to_slice())
}
fun to_string(builder: StringBuilder): String {
  builder.bytes.to_slice().to_string()
}

| Formatting  
| You should implement a write[Writer](Writer, T) function for types that can be
| printed. Writers are anything that has a write_byte(Writer, Byte) function and
| a write_bytes(Writer, Slice[Byte]) function.

fun format[T](value: T): String { "{value}" }

fun writeln[W](writer: W) { writer.write(newline) }
fun writeln[W, T](writer: W, value: T) { writer.write(value) writer.writeln() }

fun write[W, T](writer: W, value: &T) { writer.write(value.*) }

fun comma_separated[I](iter: I): CommaSeparatedFormat[I] {
  CommaSeparatedFormat { iter }
}
struct CommaSeparatedFormat[I] { iter: I }
fun write[W, T](writer: W, format: CommaSeparatedFormat[T]) {
  var first = true
  for item in format.iter do {
    if first then first = false else writer.write(", ")
    writer.write(item)
  }
}

fun buffered_writer[W](writer: W, capacity: Int): BufferedWriter[W] {
  capacity > 0 or panic("The capacity of a BufferedWriter must be positive.")
  BufferedWriter {
    buffer = filled_slice(capacity, 0.lower_byte()), len = 0, inner = writer
  }
}
struct BufferedWriter[W] { buffer: Slice[Byte], len: Int, inner: W }
fun flush[W](self: &BufferedWriter[W]) {
  self.inner.write_bytes(self.buffer.first(self.len))
  self.len = 0
}
fun amount_free[W](self: BufferedWriter[W]): Int {
  self.buffer.len - self.len
}
fun write_byte[W](self: &BufferedWriter[W], byte: Byte) {
  if self.amount_free() == 0 then self.flush()
  self.buffer.get_ref(self.len).* = byte
  self.len = self.len.inc()
}
fun write_bytes[W](self: &BufferedWriter[W], bytes: Slice[Byte]) {
  if bytes.len <= self.amount_free() then
    for byte in bytes do self.write_byte(byte)
  else {
    self.flush()
    if bytes.len <= self.amount_free()
    then for byte in bytes do self.write_byte(byte)
    else self.inner.write_bytes(bytes)
  }
}

| Debug printing  
| Just like the write function, you can also implement a write_debug function to
| customize how a value is formatted when debugging. If you don't, the compiler
| auto-generates an implementation.

fallback fun write_debug[W, T](writer: W, value: T) { ... }

fun debug[T](value: T): Debug[T] { Debug { value } }
struct Debug[T] { value: T }
fun write[W, T](writer: W, debug: Debug[T]) { writer.write_debug(debug.value) }
