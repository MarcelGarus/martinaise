fun main(): Never {
  var lines = &read_file("advent/day5.big").lines().iter()

  var ids = set[U64]()
  {
    var first_line = lines.next().unwrap()
    first_line.&.trim_prefix("seeds: ")
    for n in first_line.split(" ") do ids.&.insert(n.parse_U64())
    var _ = lines.next().unwrap()
  }

  loop {
    var category = {lines.next() orelse break}
      .split(" ").get(0).split("-to-").get(1)
    var next_ids = set[U64]()
    var mapped_ids = set[U64]()
    loop {
      var line = lines.next() orelse break
      if line.is_empty() then break
      var dest = line.split(" ").get(0).parse_U64()
      var source = line.split(" ").get(1).parse_U64()
      var len = line.split(" ").get(2).parse_U64()

      for id in ids do
        if source.to(source.add(len)).contains(id) then {
          mapped_ids.&.insert(id)
          next_ids.&.insert(id.add(dest).subtract(source))
        }
    }
    for id in ids do
      if not(mapped_ids.&.contains(id)) then next_ids.&.insert(id)
    ids = next_ids
    if category.equals("location") then break
  }
  println(ids)
  println(ids.min())

  exit(0:U8)
}
