import ../stdlib.mar

fun width(grid: Slice[Slice[U8]]): Int { grid.get(0).len }
fun height(grid: Slice[Slice[U8]]): Int { grid.len }
fun get(grid: Slice[Slice[U8]], x: I64, y: I64): U8 {
  if x < 0 or x >= grid.width() or y < 0 or y >= grid.height() then return #.
  grid.get(y).get(x)
}
fun is_digit(char: U8): Bool { '0.until('9).contains(char) }
fun is_gear(char: U8): Bool { char.equals('*) }

fun main(): Never {
  var grid = read_file("advent/day3.big").lines()
  printsln("hi")
  println(grid.width())
  println(grid.height())

  var maybe_gears = vec[Vec[Vec[Int]]]()
  for y in 0.to(grid.height()) do {
    var row = vec[Vec[Int]]()
    for x in 0.to(grid.width()) do row.&.push(vec[Int]())
    maybe_gears.&.push(row)
  }

  for y in 0_I64.to(grid.height().to_I64()) do
    for x in 0_I64.to(grid.width().to_I64()) do {
      var char = grid.get(x, y)
      if char.is_digit().and(not(grid.get(x.subtract(1_I64), y).is_digit()))
      then {
        var end = x
        loop if grid.get(end, y).is_digit() then end = end.inc() else break
        var num = grid.get(y.to_Int()).subslice(x.to_Int(), end.to_Int())
          .parse_Int()

        var adjacent = vec[Point[I64]]()
        for xx in x.dec().until(end) do {
          adjacent.&.push(Point { x = xx, y = y.dec() })
          adjacent.&.push(Point { x = xx, y = y.inc() })
        }
        adjacent.&.push(Point { x = x.dec(), y })
        adjacent.&.push(Point { x = end, y })

        for pos in adjacent do
          if grid.get(pos.x, pos.y).is_gear() then {
            prints("There might be a gear at ")
            print(pos.x.to_Int())
            prints(" ")
            println(pos.y.to_Int())
            maybe_gears.get(pos.y.to_Int()).get_ref(pos.x.to_Int()).push(num)
          }
      }
    }

  println(maybe_gears)

  var sum = 0
  for y in 0.to(grid.height()) do
    for x in 0.to(grid.width()) do {
      var adjacent_parts = maybe_gears.get(y).get(x)
      if adjacent_parts.len.equals(2)
      then sum = sum
        .add(adjacent_parts.get(0).multiply(adjacent_parts.get(1)))
    }
  println(sum)

  exit(0:U8)
}
