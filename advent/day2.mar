| returns the game id if it's possible, 0 otherwise
fun game_score(game: Slice[U8]): U64 {
  var prefix_and_turns = game.split(": ")
  var prefix = prefix_and_turns.get(0_U64)
  var turns = prefix_and_turns.get(1_U64)

  prefix.&.trim_prefix("Game ")
  var id = prefix.parse_U64()

  for turn in &turns.split("; ").iter() {
    for cubes in &turn.split(", ").iter() {
      var parts = cubes.split(" ")
      var num = parts.get(0_U64).parse_U64()
      var color = parts.get(1_U64)

      if color.equals("red").and(num.is_greater_than(12_U64)) { return 0_U64 }
      if color.equals("green").and(num.is_greater_than(13_U64)) { return 0_U64 }
      if color.equals("blue").and(num.is_greater_than(14_U64)) { return 0_U64 }
    }
  }

  return id | Game is possible
}

fun main(): U8 {
  var sum = 0_U64
  for line in &read_file("advent/day2.big").lines().iter() {
    | printsln(line)
    var score = line.game_score()
    println(score)
    sum = sum.add(score)
  }
  println(sum)
  return 0_U8
}
